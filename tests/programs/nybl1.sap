       REM  NYBL1 from CODING for the MIT-IBM 704 COMPUTER
       REM  1957
       REM  XII-15 to XII-16
       ORG 0
       FUL
       LXA 0,4         / These words are copied to 0 and 1 by
A      CPY 2,4         \ the load card sequence.
9R     TXI A,4,-1      / This copy loop brings the rest of the
       REM             | card to core-memory and terminates on
       REM             \ the end-of-record skip
9L     PZE               Used to store 9 left row.
       LTM             / We leave the trapping mode (just in case)
       REM             | and enter the loader proper. The TXI in
       REM             \ register 9R is right in any case.
RCD    RCD               Read binary card
       CPY 9L            x and n to 9L and MQ
       LLS 17            n from MQ to AC
       PAX 0,4           n to IR4
       ADD 9L          / 
       STA TRA         | x + n to TRA, CPY and ACL
       STA CPY         |
       STA ACL         \
TRA    TXL **,4          exit to x if transfer card
       CPY 9R            Check sum to 9R
       CAL 9L          / 
CPY    CPY **,4        | Words to x, x+1, ..., x+n-1
ACL    ACL **,4        | Form a new check sum
       TIX CPY,4,1     \
       SLW 9L            New check sum to 9L
       CLA 9L          /
       SUB 9R          |
       TZE RCD         | Stop if check sums disagree
       HTR RCD         \
       END  